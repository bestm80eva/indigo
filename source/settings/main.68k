EntryPoint:
    syscall OS_GETMOUSESWAP             ; Get mouse swap setting
    move.b  d7, d6
    moveq   #ID_MOUSESWAP, d7
    syscall OS_SETSTATE
    
    syscall OS_GETMOUSETYPE             ; Get mouse type setting
    move.b  d7, d6
    moveq   #ID_MOUSETYPE, d7
    syscall OS_SETSTATE
    
    syscall OS_GETMOUSESPEED            ; Get mouse sensivity
    move.b  d7, d6
    moveq   #ID_MOUSESENS, d7
    syscall OS_SETSTATE
    
    syscall OS_GETVOLUME                ; Get sound volume
    move.b  d7, d6
    beq.s   @ZeroVolume
    lsr.b   #2, d6
    addq.b  #1, d6
@ZeroVolume:
    moveq   #ID_SOUNDVOL, d7
    syscall OS_SETSTATE
    
    moveq   #0, d7                      ; Load graphics
    move.w  #NumPatterns, d6
    lea     Patterns(pc), a6
    syscall OS_LOADPATTERNS
    lea     LabelText(pc), a6
    move.w  #VramMouseButtons, d7
    syscall OS_LOADTEXTLIST
    
    moveq   #1, d7                      ; Load palettes
    lea     AltPalette(pc), a6
    syscall OS_LOADPALETTE
    moveq   #2, d7
    lea     DarkPalette(pc), a6
    syscall OS_LOADPALETTE
    moveq   #$10, d7
    syscall OS_SETBGCOLOR
    
    lea     Layout(pc), a0              ; Load interface
    syscall OS_GETDEBUG
    tst.w   d7
    beq.s   @NormalLayout
    lea     LayoutDebug(pc), a0
@NormalLayout:
    move.l   a0, a6
    syscall OS_PARSELAYOUT
    
    move.w  #$2000+VramBg, d7           ; Draw background
    move.w  #$2001+VramBg, d6
    move.w  #$2002+VramBg, a6
    move.w  #$2003+VramBg, a5
    syscall OS_FILLBACKGROUND
    
    lea     LabelList(pc), a6           ; Draw all labels
    syscall OS_DRAWLABELS
    
    moveq   #0, d7                      ; To shift some labels a bit
    moveq   #-4, d6
    move.w  d7, a6
    move.w  d6, a5
    syscall OS_SETSCROLL

;----------------------------------------------------------------------------

MainLoop:
    syscall OS_GUILOOP                  ; Let the GUI run
    
    and.w   #$FF, d7                    ; Handle the changed setting
    lsl.w   #2, d7
    lea     @Handlers(pc), a6
    jsr     (a6,d7.w)
    
    bra.s   MainLoop

;----------------------------------------------------------------------------

@Handlers:
    bra.w   SetMouseSwap                ; ID_MOUSESWAP
    bra.w   SetMouseType                ; ID_MOUSETYPE
    bra.w   SetSensivity                ; ID_MOUSESENS
    bra.w   DecreaseSensivity           ; ID_MOUSESENS_D
    bra.w   IncreaseSensivity           ; ID_MOUSESENS_U
    bra.w   SetVolume                   ; ID_SOUNDVOL
    bra.w   DecreaseVolume              ; ID_SOUNDVOL_D
    bra.w   IncreaseVolume              ; ID_SOUNDVOL_U
    illegal                             ; ID_TEST

@Dummy:
    rts
