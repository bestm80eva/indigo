EntryPoint:
    moveq   #0, d7                      ; Load graphics
    move.w  #NumPatterns, d6
    lea     Patterns(pc), a6
    syscall OS_LOADPATTERNS
    moveq   #2, d7
    lea     InvPalette(pc), a6
    syscall OS_LOADPALETTE
    moveq   #1, d7
    lea     OpenPalette(pc), a6
    syscall OS_LOADPALETTE
    
    lea     Layout(pc), a6              ; Load interface
    syscall OS_PARSELAYOUT
    
    moveq   #FLIST_X-1, d7              ; Draw frame for file list
    moveq   #FLIST_Y-1, d6
    moveq   #0, d5
    move.w  #FLIST_W, a6
    move.w  #FLIST_H, a5
    move.w  #VramFrame, a4
    syscall OS_FILLFRAME
    
    moveq   #FLIST_X-1, d7              ; Placeholder where filename will go
    moveq   #FLIST_Y-3, d6
    moveq   #0, d5
    move.w  #FLIST_W, a6
    move.w  #VramFilename, a5
    syscall OS_DRAWHBAR
    
    ;move.w  #'/'<<8, (CurrDir)          ; Default path
    move.l  #'/rom', (CurrDir)          ; Temp default path
    ;move.w  #'/'<<8, (CurrDir+4)
    move.b  #0, (CurrDir+4)
    
    bsr     LoadDirectory               ; Load initial files
    ;bsr     MakeTestList

;----------------------------------------------------------------------------

MainLoop:
    syscall OS_GUILOOP                  ; Let the GUI run
    
    and.w   #$FF, d7                    ; Jump to the relevant event handler
    lsl.w   #2, d7
    lea     @Handlers(pc), a6
    lea     (a6,d7.w), a6
    jsr     (a6)
    
    bra.s   MainLoop

;----------------------------------------------------------------------------

@Handlers:
    bra.w   FileListClick               ; ID_FILELIST
    bra.w   ScrollerMoved               ; ID_SCROLL
    bra.w   ScrollUp                    ; ID_SCROLL_U
    bra.w   ScrollDown                  ; ID_SCROLL_D
    bra.w   GoToParent                  ; ID_PARENT
    bra.w   @Dummy                      ; ID_OPEN

@Dummy:
    rts

;****************************************************************************
; MakeTestList
; Adds fake entries to the file list. Will be removed once we have real
; directories to be displayed.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

MakeTestList:
    bsr     ClearFileList
    
    moveq   #5-1, d2
@Loop2:
    moveq   #1, d1
    moveq   #4-1, d0
@Loop:
    move.w  d1, d7
    lea     @FakeFile1(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile2(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile3(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile4(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile5(pc), a6
    bsr     AddFileEntry
    addq.w  #1, d1
    dbf     d0, @Loop
    dbf     d2, @Loop2
    
    bsr     GetFileListReady            ; Done adding files
    bra     RefreshFileList             ; Redraw the list
    rts

;----------------------------------------------------------------------------

@FakeFile1:         dc.b '68000.text',0
@FakeFile2:         dc.b 'z80.text',0
@FakeFile3:         dc.b 'video.text',0
@FakeFile4:         dc.b 'sound.text',0
@FakeFile5:         dc.b 'joypad.text',0
                    even

;****************************************************************************
; LoadDirectory
; Loads the file list from the current directory.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

LoadDirectory:
    lea     (CurrDir), a6               ; Open the current directory
    syscall OS_OPENDIR
    tst.b   d7
    beq.s   @Error
    
    move.w  d7, -(sp)                   ; Init the list
    bsr     ClearFileList
    move.w  (sp)+, d0
    
@Loop:
    move.b  d0, d7                      ; Read next entry
    syscall OS_READDIR
    cmp.w   #$FFFF, d7
    beq.s   @End
    
    tst.w   d7                          ; Which icon to show? (for now just
    spl.b   d7                          ; tell apart folders from files)
    and.w   #1, d7
    addq.w  #1, d7
    
    bsr     AddFileEntry                ; Add file to the list
    bra.s   @Loop                       ; Keep reading!
    
@End:
    move.b  d0, d7                      ; Done with directory
    syscall OS_CLOSEDIR
    
    bsr     GetFileListReady            ; Done adding files
    bra     RefreshFileList             ; Redraw the list

;----------------------------------------------------------------------------

@Error:
    illegal                             ; For now...

;****************************************************************************
; GoToParent
; Goes to the parent directory.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

GoToParent:
    lea     (CurrDir), a0               ; Check that it's not the root (we
    cmpi.w  #'/'<<8, (a0)               ; can't go up from here)
    bne.s   @NotRoot
    rts
@NotRoot:
    
    lea     1(a0), a1                   ; Find where the last separator is
    moveq   #'/', d6                    ; (note that we have to handle the
@FindLoop:                              ; special case where the path ends in
    move.b  (a0)+, d7                   ; /, which shouldn't be counted)
    beq.s   @FindEnd
    cmp.b   d6, d7
    bne.s   @FindLoop
    tst.b   (a0)
    beq.s   @FindEnd
    move.l  a0, a1
    bra.s   @FindLoop
@FindEnd:
    
    clr.b   (a1)                        ; Cut the path at the last separator
    bra     LoadDirectory               ; Load the new directory
