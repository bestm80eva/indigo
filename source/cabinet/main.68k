EntryPoint:
    moveq   #0, d7                      ; Load graphics
    move.w  #NumPatterns, d6
    lea     Patterns(pc), a6
    syscall OS_LOADPATTERNS
    moveq   #1, d7
    lea     InvPalette(pc), a6
    syscall OS_LOADPALETTE
    
    lea     Layout(pc), a6              ; Load interface
    syscall OS_PARSELAYOUT
    
    moveq   #FLIST_X-2, d7              ; Draw frame for file list
    moveq   #FLIST_Y-1, d6
    moveq   #0, d5
    move.w  #FLIST_W+1, a6
    move.w  #FLIST_H, a5
    move.w  #VramFrame, a4
    syscall OS_FILLFRAME
    
    bsr     ClearFileList               ; Set up the file list
    bsr     MakeTestList
    bsr     GetFileListReady
    bsr     RefreshFileList

;----------------------------------------------------------------------------

MainLoop:
    syscall OS_GUILOOP                  ; Let the GUI run
    
    and.w   #$FF, d7                    ; Jump to the relevant event handler
    lsl.w   #2, d7
    lea     @Handlers(pc), a6
    lea     (a6,d7.w), a6
    jsr     (a6)
    
    bra.s   MainLoop

;----------------------------------------------------------------------------

@Handlers:
    bra.w   @Dummy                      ; ID_FILELIST
    bra.w   ScrollerMoved               ; ID_SCROLL
    bra.w   ScrollUp                    ; ID_SCROLL_U
    bra.w   ScrollDown                  ; ID_SCROLL_D

@Dummy:
    rts

;****************************************************************************
; MakeTestList
; Adds fake entries to the file list. Will be removed once we have real
; directories to be displayed.
;----------------------------------------------------------------------------
; breaks: all
;****************************************************************************

MakeTestList:
    moveq   #5-1, d2
@Loop2:
    moveq   #1, d1
    moveq   #3-1, d0
@Loop:
    move.w  d1, d7
    lea     @FakeFile1(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile2(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile3(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile4(pc), a6
    bsr     AddFileEntry
    move.w  d1, d7
    lea     @FakeFile5(pc), a6
    bsr     AddFileEntry
    addq.w  #1, d1
    dbf     d0, @Loop
    dbf     d2, @Loop2
    
    rts

;----------------------------------------------------------------------------

@FakeFile1:         dc.b '68000.text',0
@FakeFile2:         dc.b 'z80.text',0
@FakeFile3:         dc.b 'video.text',0
@FakeFile4:         dc.b 'sound.text',0
@FakeFile5:         dc.b 'joypad.text',0
                    even
